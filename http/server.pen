import Core'Number
import Core'String
import Core'String'Byte
import Core'String'Utf8
import Http'Context { Context }
import Http'Request { Request }
import Http'Response { Response }
import Http'Server
import Regex'Expression as Regex

import 'application { Application }
import 'application'metric { Metric }
import 'application'record { Record }
import 'json'metric
import 'json'record
import 'logger { Logger }

type Server {
  context Context
  application Application
  logger Logger
}

New = \(ctx Context, a Application, l Logger) Server {
  Server{context: ctx, application: a, logger: l}
}

Serve = \(s Server, address string) none | error {
  logger'Info(s.logger, "serving at " + address)

  Server'Serve(
    s.context,
    address,
    \(req Request) Response {
      res = if [resource, ...ss] = Utf8'Split(Utf8'TrimStartMatches(req.Uri, "/"), "/") {
        if resource() == "metrics" {
          if [id, ...ss] = ss {
            if [resource, ...ss] = ss {
              if resource() == "records" {
                if req.Method == "GET" {
                  findRecords(s.application, id())
                } else if req.Method == "POST" {
                  createRecord(s.application, req)
                } else {
                  notFound()
                }
              } else {
                notFound()
              }
            } else {
              if req.Method == "GET" {
                findMetric(s.application, id())
              } else {
                notFound()
              }
            }
          } else {
            if req.Method == "GET" {
              findMetrics(s.application)
            } else if req.Method == "POST" {
              createMetric(s.application, req)
            } else {
              notFound()
            }
          }
        } else {
          notFound()
        }
      } else {
        notFound()
      }
      logger'Info(
        s.logger,
        req.Method
          + " "
          + req.Uri
          + " - "
          + Number'String(res.Status)
          + " "
          + Number'String(Byte'Length(res.Body)),
      )

      res
    },
  )
}

findMetric = \(a Application, id string) Response {
  if x = application'FindMetric(a, id) as Metric {
    getResponse(metric'Serialize(x))
  } else {
    errorResponse(x)
  }
}

createMetric = \(a Application, r Request) Response {
  if x = \() none | error {
    application'CreateMetric(a, metric'DeserializeInput(r.Body)?)
  }() as none {
    postResponse()
  } else {
    errorResponse(x)
  }
}

findRecords = \(a Application, metricId string) Response {
  if x = application'FindRecords(a, metricId) as [Record] {
    getResponse(record'SerializeMany(x))
  } else {
    errorResponse(x)
  }
}

createRecord = \(a Application, r Request) Response {
  if x = \() none | error {
    application'CreateRecord(a, record'Deserialize(r.Body)?)
  }() as none {
    postResponse()
  } else {
    errorResponse(x)
  }
}

getResponse = \(s string) Response {
  Response{
    Status: 200,
    Headers: contentHeaders(),
    Body: s,
  }
}

postResponse = \() Response {
  Response{
    Status: 201,
    Headers: {string: string},
    Body: "",
  }
}

# TODO Handle 500 errors properly.
errorResponse = \(e error) Response {
  if e = source(e) as string {
    Response{
      Status: 400,
      Headers: {string: string},
      Body: e,
    }
  } else {
    Response{
      Status: 500,
      Headers: {string: string},
      Body: "",
    }
  }
}

contentHeaders = \() {string: string} {
  {string: string "content-type": "text/json"}
}

notFound = \() Response {
  Response{
    Status: 404,
    Headers: {string: string},
    Body: "",
  }
}
