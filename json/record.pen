import Core'String
import Json'Decode { Decode }
import Json'Encode { Encode }
import Json'Value { Value }

import 'application'record { Record }
import 'json'utility

Serialize = \(r Record) string {
  Encode(
    Value'New(
      {string: Value
        "metricId": Value'New(r.MetricId),
        "commitHash": Value'New(r.CommitHash),
        "time": Value'New(r.Time),
      },
    ),
  )
}

SerializeMany = \(rs [Record]) string {
  "[" + String'Join([string Serialize(r()) for r in rs], ",") + "]"
}

Deserialize = \(s string) Record | error {
  o = utility'Object(Decode(s)?)?

  Record{
    MetricId: utility'String(utility'Field(o, "metricId")?)?,
    CommitHash: utility'String(utility'Field(o, "commitHash")?)?,
    Time: utility'Number(utility'Field(o, "time")?)?,
  }
}

DeserializeInput = \(s string, metricId string) Record | error {
  o = utility'Object(Decode(s)?)?

  Record{
    MetricId: metricId,
    CommitHash: utility'String(utility'Field(o, "commitHash")?)?,
    Time: utility'Number(utility'Field(o, "time")?)?,
  }
}
