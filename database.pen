import Core'String
import Sql'Context { Context }
import Sql'Pool { Pool }
import Sql'Pool'Options { Options }

type Value = boolean | none | number | string

type Database {
  context Context
  pool Pool
}

defaultOptions = \() Options {
  Options{
    MinConnections: 8,
    MaxConnections: 512,
    ConnectTimeout: 500,
  }
}

New = \(ctx Context, uri string) Database | error {
  Database{
    context: ctx,
    pool: Pool'New(ctx, uri, defaultOptions())?,
  }
}

Find = \(s Database, table string, where {string: Value}) {string: Value} | error {
  ks = [string k for k, _ in where]
  xs = Pool'Query(s.context, s.pool, findQuery(table, ks) + " limit 1", [Value v for _, v in where])?

  if [x, ..._] = xs {
    buildValue(ks, x())
  } else {
    error(table + " not found")
  }
}

FindMany = \(s Database, table string, where {string: Value}) [{string: Value}] | error {
  ks = [string k for k, _ in where]
  debug(findQuery(table, ks))
  xs = Pool'Query(s.context, s.pool, findQuery(table, ks), [Value v for _, v in where])?

  [{string: Value} buildValue(ks, x()) for x in xs]
}

findQuery = \(table string, ks [string]) string {
  "select * from " + table
    + if size(ks) > 0 {
      " where " + String'Join([string k() + " = ?" for k in ks], " and ")
    } else {
      ""
    }
}

Create = \(s Database, table string, x {string: Value}) none | error {
  Pool'Execute(
    s.context,
    s.pool,
    saveQuery(table, [string k for k, _ in x]),
    [Value v for _, v in x],
  )?

  none
}

saveQuery = \(table string, ks [string]) string {
  "insert into " + table
    + "("
    + String'Join(ks, ",")
    + ") values ("
    + String'Join([string "?" for _ in ks], ",")
    + ")"
}

buildValue = \(ks [string], vs [Value]) {string: Value} {
  if [k, ...ks] = ks {
    if [v, ...vs] = vs {
      {string: Value ...buildValue(ks, vs), k(): v()}
    } else {
      {string: Value}
    }
  } else {
    {string: Value}
  }
}
